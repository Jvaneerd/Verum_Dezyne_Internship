import LED.dzn;
import Timer.dzn;
import Siren.dzn;
import PWManager.dzn;
import Sensor.dzn;

interface IController {
  in void passwordEntered(String pw);

  behaviour {
    on passwordEntered: {}
  }
}

component Controller {
  provides IController iController;
  requires ISiren iSiren;
  requires ILED iLed;
  requires ITimer iTimer;
  requires IPWManager iPWManager;
  requires ISensor iSensor;

  behaviour {
    enum State { Unarmed, Armed, Alarming };
    State state = State.Unarmed;

    [state.Unarmed] {
      on iController.passwordEntered(pw): {
      	bool valid = iPWManager.verifyPassword(pw);
      	if(valid) {
	        state = State.Armed;
	        iLed.setYellow();
	        iSensor.turnOn();
        }
      }
      on iSensor.triggered(): {}
      on iTimer.timeout(): illegal;
    }
    [state.Armed] {
      on iController.passwordEntered(pw): {
      	bool valid = iPWManager.verifyPassword(pw);
      	if(valid) {
	        state = State.Unarmed;
	        iLed.setGreen();
	        iSensor.turnOff();
      	}
      }
      on iSensor.triggered(): {
      	state = State.Alarming;
      	iTimer.start($30000$);
      	iLed.setRed();
      	iSensor.turnOff();
      }
      on iTimer.timeout(): illegal;
    }
    [state.Alarming] {
      on iController.passwordEntered(pw): {
      	bool valid = iPWManager.verifyPassword(pw);
      	if(valid) {
	        state = State.Armed;
	        iLed.setYellow();
	        iSiren.turnOff();
	        iTimer.cancel();
	        iSensor.turnOn();
        }
      }
      on iSensor.triggered(): {}
      on iTimer.timeout(): {
      	iSiren.turnOn();
      }
    }
  }
}
