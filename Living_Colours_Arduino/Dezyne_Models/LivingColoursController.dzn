import RGBLed.dzn;
import DemoMode.dzn;
import Mixer.dzn;
import Presetter.dzn;

interface IController {
	in void begin();
	in void mixButtonPressed();
	in void presetButtonPressed();
	
	behaviour {
		bool initialized = false;
		
		on begin: {
			[!initialized] initialized = true;
			[initialized] illegal;
		}
		
		[initialized] {
			on mixButtonPressed: {}
			on presetButtonPressed: {}
		}
		
		[!initialized] {
			on mixButtonPressed: illegal;
			on presetButtonPressed: illegal;
		}
	}
}

component Controller {
	provides IController iController;
	requires IDemoMode iDemoMode;
	requires IMixer iMixer;
	requires IPresetter iPresetter;
	
	behaviour {
		enum State { Demo, Mixing, Presets };
		bool initialized = false;
		
		State state = State.Demo;
		
		on iController.begin(): {
			[initialized] illegal;
			[!initialized] {
				initialized = true;
				iDemoMode.start();
			}
		}
		
		[initialized] {
			on iController.mixButtonPressed(): {
				[state == State.Mixing] {
					iMixer.toggle();
				}
				[state == State.Demo] {
					iDemoMode.stop();
					iMixer.start();
					state = State.Mixing;
				} 
				[state == State.Presets] {
					iPresetter.stop();
					iMixer.start();
					state = State.Mixing;
				}
			} 
		
			on iController.presetButtonPressed(): {
				[state == State.Presets] {
					iPresetter.toggle();
				}
				[state == State.Demo] {
					iDemoMode.stop();
					iPresetter.start();
					state = State.Presets;
				} 
				[state == State.Mixing] {
					iPresetter.start();
					iMixer.stop();
					state = State.Presets;
				}
			}
		}
		
	}
}

component LivingColours {
	provides IController iController;
	requires ITimer iTimer;
	requires IRGBLed iDemoRGBLed;
	requires IRGBLed iMixerRGBLed;
	requires IPresetSelector iPresetSelector;
	requires IHWMixer iHWMixer;
	
	system {
		Controller controller;
		DemoSystem demoSystem;
		Mixer mixer;
		Presetter presetter;
		
		iController<=>controller.iController;
		controller.iDemoMode<=>demoSystem.iDemoMode;
		controller.iMixer<=>mixer.iMixer;
		controller.iPresetter<=>presetter.iPresetter;
		demoSystem.iRGBLed<=>iDemoRGBLed;
		demoSystem.iTimer<=>iTimer;
		presetter.iPresetSelector<=>iPresetSelector;
		mixer.iHWMixer<=>iHWMixer;
		mixer.iRGBLed<=>iMixerRGBLed;
	}
}