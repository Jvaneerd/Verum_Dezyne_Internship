extern Preset $uint8_t$;

interface IPresetter {
	in void start();
	in void stop();
	in void toggle();
	
	behaviour {
		bool isRunning = false;
		on start: {
			[!isRunning] isRunning = true;
			[isRunning] illegal;
		}
		on stop: {
			[!isRunning] illegal;
			[isRunning] isRunning = false;
		}
		on toggle: {
			[!isRunning] illegal;
			[isRunning] {}
		}
	}
}

interface IPresetSelector {
	in void selectPreset(Preset p);
	in void stop();
	
	behaviour {
		on selectPreset: {}
		on stop: {}
	}
}

component Presetter {
	provides IPresetter iPresetter;
	requires IPresetSelector iPresetSelector;
	
	behaviour {
		enum Presets { Yellow, Cyan, Magenta };
		Presets preset = Presets.Yellow;
		bool isRunning = false;
		
		on iPresetter.start(): {
			[!isRunning] {
				isRunning = true;
				preset = Presets.Yellow;
				iPresetSelector.selectPreset($self->preset$);
			}
		}
		
		on iPresetter.stop(): {
			[isRunning] {
				isRunning = false;
				iPresetSelector.stop();
			} 
		}
			
		on iPresetter.toggle(): {
			[isRunning] {
				[preset == Presets.Yellow] {
					preset = Presets.Cyan;
					iPresetSelector.selectPreset($self->preset$);
				}
				[preset == Presets.Cyan] {
					preset = Presets.Magenta;
					iPresetSelector.selectPreset($self->preset$);
				}
				[preset == Presets.Magenta] {
					preset = Presets.Yellow;
					iPresetSelector.selectPreset($self->preset$);
				}
			} 
		}
	}
}
